;;; flash-search-test.el --- Tests for flash-search -*- lexical-binding: t -*-

;;; Commentary:
;; ERT tests for flash-search module.

;;; Code:

(require 'ert)
(require 'flash-state)
(require 'flash-search)

(ert-deftest flash-search-empty-pattern-test ()
  "Test search with empty pattern returns no matches."
  (with-temp-buffer
    (insert "foo bar baz")
    (let ((state (flash-state-create (list (selected-window)))))
      (setf (flash-state-pattern state) "")
      (flash-search state)
      (should (null (flash-state-matches state))))))

(ert-deftest flash-search-basic-test ()
  "Test basic search finds matches."
  (with-temp-buffer
    (insert "foo bar foo baz foo")
    (goto-char (point-min))
    ;; Need to display buffer in window for window-start/end to work
    (set-window-buffer (selected-window) (current-buffer))
    (let ((state (flash-state-create (list (selected-window)))))
      (setf (flash-state-pattern state) "foo")
      (flash-search state)
      (should (= 3 (length (flash-state-matches state))))
      ;; Check first match
      (let ((first-match (car (flash-state-matches state))))
        (should (= 1 (marker-position (flash-match-pos first-match))))
        (should (= 4 (marker-position (flash-match-end-pos first-match))))
        (should (eq (selected-window) (flash-match-window first-match)))))))

(ert-deftest flash-search-case-insensitive-test ()
  "Test search is case-insensitive."
  (with-temp-buffer
    (insert "Foo FOO foo")
    (goto-char (point-min))
    (set-window-buffer (selected-window) (current-buffer))
    (let ((state (flash-state-create (list (selected-window)))))
      (setf (flash-state-pattern state) "foo")
      (flash-search state)
      (should (= 3 (length (flash-state-matches state)))))))

(ert-deftest flash-search-no-match-test ()
  "Test search with no matches."
  (with-temp-buffer
    (insert "foo bar baz")
    (goto-char (point-min))
    (set-window-buffer (selected-window) (current-buffer))
    (let ((state (flash-state-create (list (selected-window)))))
      (setf (flash-state-pattern state) "xyz")
      (flash-search state)
      (should (null (flash-state-matches state))))))

(ert-deftest flash-search-markers-test ()
  "Test that matches use markers."
  (with-temp-buffer
    (insert "test")
    (goto-char (point-min))
    (set-window-buffer (selected-window) (current-buffer))
    (let ((state (flash-state-create (list (selected-window)))))
      (setf (flash-state-pattern state) "test")
      (flash-search state)
      (let ((match (car (flash-state-matches state))))
        (should (markerp (flash-match-pos match)))
        (should (markerp (flash-match-end-pos match)))))))

(ert-deftest flash-search-skips-dead-window-test ()
  "Test search skips windows that were deleted during a flash session."
  (let ((buf (generate-new-buffer "*flash-search-dead-window*"))
        win)
    (unwind-protect
        (progn
          (setq win (split-window-right))
          (with-current-buffer buf
            (insert "foo bar"))
          (set-window-buffer win buf)
          (delete-window win)
          (let ((state (flash-state-create (list win))))
            (setf (flash-state-pattern state) "foo")
            (flash-search state)
            (should-not (flash-state-matches state))))
      (when (window-live-p win)
        (delete-window win))
      (kill-buffer buf))))

(ert-deftest flash-get-fold-at-visible-test ()
  "Test fold detection returns nil for visible text."
  (with-temp-buffer
    (insert "visible text")
    (should (null (flash--get-fold-at 5)))))

(provide 'flash-search-test)
;;; flash-search-test.el ends here
